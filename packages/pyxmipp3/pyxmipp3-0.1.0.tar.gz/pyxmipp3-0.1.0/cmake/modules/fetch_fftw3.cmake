cmake_minimum_required(VERSION 3.16)

include(FetchContent)
include(GNUInstallDirs)

function(fetch_fftw3)
	set(FFTW3_URL https://fftw.org/fftw-3.3.10.tar.gz)
	set(FFTW3_INSTALL_DIR ${CMAKE_CURRENT_BINARY_DIR}/external/fftw3)
	set(FFTW3_INCLUDE_DIR ${FFTW3_INSTALL_DIR}/${CMAKE_INSTALL_INCLUDEDIR})
	set(FFTW3_FLOAT_STATIC_LIBRARY ${FFTW3_INSTALL_DIR}/${CMAKE_INSTALL_LIBDIR}/${CMAKE_STATIC_LIBRARY_PREFIX}fftw3f${CMAKE_STATIC_LIBRARY_SUFFIX})
	set(FFTW3_FLOAT_THREADS_STATIC_LIBRARY ${FFTW3_INSTALL_DIR}/${CMAKE_INSTALL_LIBDIR}/${CMAKE_STATIC_LIBRARY_PREFIX}fftw3f_threads${CMAKE_STATIC_LIBRARY_SUFFIX})
	set(FFTW3_DOUBLE_STATIC_LIBRARY ${FFTW3_INSTALL_DIR}/${CMAKE_INSTALL_LIBDIR}/${CMAKE_STATIC_LIBRARY_PREFIX}fftw3${CMAKE_STATIC_LIBRARY_SUFFIX})
	set(FFTW3_DOUBLE_THREADS_STATIC_LIBRARY ${FFTW3_INSTALL_DIR}/${CMAKE_INSTALL_LIBDIR}/${CMAKE_STATIC_LIBRARY_PREFIX}fftw3_threads${CMAKE_STATIC_LIBRARY_SUFFIX})
	file(MAKE_DIRECTORY ${FFTW3_INCLUDE_DIR})

	cmake_policy(SET CMP0135 NEW) # To avoid warnings
	ExternalProject_Add(
		fftw3_float_external
		URL ${FFTW3_URL}
		PREFIX ${CMAKE_CURRENT_BINARY_DIR}/fftw3_float_build
    	INSTALL_DIR ${FFTW3_INSTALL_DIR}
		CMAKE_ARGS
			-DCMAKE_INSTALL_PREFIX=${FFTW3_INSTALL_DIR}
			-DCMAKE_POSITION_INDEPENDENT_CODE=ON
			-DCMAKE_POLICY_VERSION_MINIMUM=3.5 # Fixes compilation error on CMake 4.x
			-DBUILD_SHARED_LIBS=OFF
			-DBUILD_TESTS=OFF
			-DENABLE_THREADS=ON
			-DENABLE_FLOAT=ON
		LOG_DOWNLOAD ON
		LOG_CONFIGURE ON
		LOG_BUILD ON
		BUILD_BYPRODUCTS
			${FFTW3_FLOAT_STATIC_LIBRARY}
			${FFTW3_FLOAT_THREADS_STATIC_LIBRARY}
	)
	ExternalProject_Add(
		fftw3_double_external
		URL ${FFTW3_URL}
		PREFIX ${CMAKE_CURRENT_BINARY_DIR}/fftw3_double_build
    	INSTALL_DIR ${FFTW3_INSTALL_DIR}
		CMAKE_ARGS
			-DCMAKE_INSTALL_PREFIX=${FFTW3_INSTALL_DIR}
			-DCMAKE_POSITION_INDEPENDENT_CODE=ON
			-DCMAKE_POLICY_VERSION_MINIMUM=3.5 # Fixes compilation error on CMake 4.x
			-DBUILD_SHARED_LIBS=OFF
			-DBUILD_TESTS=OFF
			-DENABLE_THREADS=ON
		LOG_DOWNLOAD ON
		LOG_CONFIGURE ON
		LOG_BUILD ON
		BUILD_BYPRODUCTS
			${FFTW3_DOUBLE_STATIC_LIBRARY}
			${FFTW3_DOUBLE_THREADS_STATIC_LIBRARY}
	)

	add_library(FFTW3::Float STATIC IMPORTED)
	set_target_properties(FFTW3::Float PROPERTIES
		IMPORTED_LOCATION ${FFTW3_FLOAT_STATIC_LIBRARY}
		INTERFACE_INCLUDE_DIRECTORIES ${FFTW3_INCLUDE_DIR}
	)
	add_dependencies(FFTW3::Float fftw3_float_external)

	add_library(FFTW3::FloatThreads STATIC IMPORTED)
	set_target_properties(FFTW3::FloatThreads PROPERTIES
		IMPORTED_LOCATION ${FFTW3_FLOAT_THREADS_STATIC_LIBRARY}
		INTERFACE_INCLUDE_DIRECTORIES ${FFTW3_INCLUDE_DIR}
	)
	add_dependencies(FFTW3::FloatThreads fftw3_float_external)

	add_library(FFTW3::Double STATIC IMPORTED)
	set_target_properties(FFTW3::Double PROPERTIES
		IMPORTED_LOCATION ${FFTW3_DOUBLE_STATIC_LIBRARY}
		INTERFACE_INCLUDE_DIRECTORIES ${FFTW3_INCLUDE_DIR}
	)
	add_dependencies(FFTW3::Double fftw3_double_external)

	add_library(FFTW3::DoubleThreads STATIC IMPORTED)
	set_target_properties(FFTW3::DoubleThreads PROPERTIES
		IMPORTED_LOCATION ${FFTW3_DOUBLE_THREADS_STATIC_LIBRARY}
		INTERFACE_INCLUDE_DIRECTORIES ${FFTW3_INCLUDE_DIR}
	)
	add_dependencies(FFTW3::DoubleThreads fftw3_double_external)
endfunction()
