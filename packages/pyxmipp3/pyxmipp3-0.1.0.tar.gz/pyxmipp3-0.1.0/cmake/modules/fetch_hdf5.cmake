cmake_minimum_required(VERSION 3.16)

include(FetchContent)
include(GNUInstallDirs)

function(fetch_hdf5)
	set(HDF5_INSTALL_DIR ${CMAKE_CURRENT_BINARY_DIR}/external/hdf5)
	set(HDF5_INCLUDE_DIR ${HDF5_INSTALL_DIR}/${CMAKE_INSTALL_INCLUDEDIR})
	set(HDF5_STATIC_LIBRARY ${HDF5_INSTALL_DIR}/lib/${CMAKE_STATIC_LIBRARY_PREFIX}hdf5${CMAKE_STATIC_LIBRARY_SUFFIX})
	set(HDF5_CPP_STATIC_LIBRARY ${HDF5_INSTALL_DIR}/lib/${CMAKE_STATIC_LIBRARY_PREFIX}hdf5_cpp${CMAKE_STATIC_LIBRARY_SUFFIX})
	file(MAKE_DIRECTORY ${HDF5_INCLUDE_DIR})

	cmake_policy(SET CMP0135 NEW) # To avoid warnings
	ExternalProject_Add(
		hdf5_external
		URL https://github.com/HDFGroup/hdf5/archive/refs/tags/hdf5-1_14_3.tar.gz
		PREFIX ${CMAKE_CURRENT_BINARY_DIR}/hdf5_build
    	INSTALL_DIR ${HDF5_INSTALL_DIR}
		CMAKE_ARGS
			-DCMAKE_INSTALL_PREFIX=${HDF5_INSTALL_DIR}
			-DCMAKE_POSITION_INDEPENDENT_CODE=ON
			-DBUILD_SHARED_LIBS=OFF
			-DBUILD_STATIC_LIBS=ON
			-DBUILD_TESTING=OFF
			-DHDF5_BUILD_CPP_LIB=ON
		LOG_DOWNLOAD ON
		LOG_CONFIGURE ON
		LOG_BUILD ON
		BUILD_BYPRODUCTS
			${HDF5_STATIC_LIBRARY}
			${HDF5_CPP_STATIC_LIBRARY}
	)

	add_library(HDF5::HDF5 STATIC IMPORTED)
	set_target_properties(HDF5::HDF5 PROPERTIES
		IMPORTED_LOCATION ${HDF5_STATIC_LIBRARY}
		INTERFACE_INCLUDE_DIRECTORIES ${HDF5_INCLUDE_DIR}
	)
	add_dependencies(HDF5::HDF5 hdf5_external)

	add_library(HDF5::CXX STATIC IMPORTED)
	set_target_properties(HDF5::CXX PROPERTIES
		IMPORTED_LOCATION ${HDF5_CPP_STATIC_LIBRARY}
		INTERFACE_INCLUDE_DIRECTORIES ${HDF5_INCLUDE_DIR}
		INTERFACE_LINK_LIBRARIES HDF5::HDF5
	)
	add_dependencies(HDF5::CXX hdf5_external)
endfunction()
