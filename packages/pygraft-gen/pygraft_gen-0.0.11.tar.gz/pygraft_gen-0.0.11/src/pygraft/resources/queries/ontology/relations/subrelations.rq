#  Software Name: PyGraft-gen
#  SPDX-FileCopyrightText: Copyright (c) Orange SA
#  SPDX-License-Identifier: MIT
#
#  This software is distributed under the MIT license, the text of which is available at https://opensource.org/license/MIT/ or see the "LICENSE" file for more details.
#
#  Authors: See CONTRIBUTORS.txt
#  Software description: A RDF Knowledge Graph stochastic generation solution.
#
# --------------------------------------------------------------------------------------------------
# FILE      : subrelations.rq
# AUTHOR    : Ovidiu PASCAL - Orange INNOV
# DATE      : 2025-12-12
# PURPOSE   : Retrieve explicit rdfs:subPropertyOf links between ontology-local
#             object-style properties as pairs (?subprop_uri -> ?superprop_uri).
#
# INTENT    :
#   Extract the explicit, one-hop subproperty hierarchy between relations for
#   relation_info extraction and KG generation.
#
#   Both sides of the subPropertyOf link are required to belong to the shared
#   ontology-local relations universe defined in relations_seed.rq and injected
#   via the marker line:
#
#       # @RELATIONS_SEED
#
#   No transitive closure or OWL reasoning is applied here.
#
# OUTPUT    :
#   - ?subprop_uri   : IRI of the sub-property (ontology-local, object-style)
#   - ?superprop_uri : IRI of the super-property (ontology-local, object-style)
#
# LOGIC     :
#   1) Select explicit rdfs:subPropertyOf triples (one hop only).
#   2) Enforce that both sub-property and super-property belong to the shared
#      relations universe by re-running the seed in nested subqueries and
#      re-binding its output.
#   3) Keep only IRI-valued properties on both sides.
#
# NOTES     :
#   - Pure SPARQL 1.1; no vendor extensions.
#   - Only asserted rdfs:subPropertyOf triples are returned.
#   - Transitive closure (rdfs:subPropertyOf*) must be computed later as a
#     derived structure if needed.
# --------------------------------------------------------------------------------------------------

PREFIX rdf:  <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX owl:  <http://www.w3.org/2002/07/owl#>
PREFIX xsd:  <http://www.w3.org/2001/XMLSchema#>

SELECT DISTINCT
  ?subprop_uri
  ?superprop_uri
WHERE {
  ### 1) Explicit subPropertyOf link (one hop only)
  ?subprop_uri rdfs:subPropertyOf ?superprop_uri .
  FILTER(isIRI(?subprop_uri) && isIRI(?superprop_uri))

  ### 2) Require the sub-property to belong to the shared relations universe
  {
    SELECT DISTINCT ?subprop_uri
    WHERE {
      # @RELATIONS_SEED
      BIND(?prop_uri AS ?subprop_uri)
    }
  }

  ### 3) Require the super-property to belong to the same relations universe
  {
    SELECT DISTINCT ?superprop_uri
    WHERE {
      # @RELATIONS_SEED
      BIND(?prop_uri AS ?superprop_uri)
    }
  }
}
ORDER BY
  STR(?subprop_uri)
  STR(?superprop_uri)
