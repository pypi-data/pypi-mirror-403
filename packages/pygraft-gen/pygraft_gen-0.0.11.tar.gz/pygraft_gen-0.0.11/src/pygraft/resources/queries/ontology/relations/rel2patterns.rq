#  Software Name: PyGraft-gen
#  SPDX-FileCopyrightText: Copyright (c) Orange SA
#  SPDX-License-Identifier: MIT
#
#  This software is distributed under the MIT license, the text of which is available at https://opensource.org/license/MIT/ or see the "LICENSE" file for more details.
#
#  Authors: See CONTRIBUTORS.txt
#  Software description: A RDF Knowledge Graph stochastic generation solution.
#
# --------------------------------------------------------------------------------------------------
# FILE      : rel2patterns.rq
# AUTHOR    : Ovidiu PASCAL - Orange INNOV
# DATE      : 2025-12-11
# PURPOSE   : Retrieve ontology-local object-style properties and their explicitly
#             declared OWL characteristic types (patterns).
#
# INTENT    :
#   This query profiles relations by extracting OWL characteristic declarations
#   (e.g. symmetric, transitive, functional) that are explicitly stated in the
#   ontology graph.
#
#   The universe of properties is not defined here. Instead, it is delegated to
#   relations_seed.rq and injected via the {{RELATIONS_SEED}} placeholder so that
#   all relation extractors operate over the same ontology-local property universe.
#
#   No OWL reasoning or inference is performed: only explicit rdf:type assertions
#   present in the ontology are considered.
#
# OUTPUT    :
#   - ?prop_uri    : IRI of an ontology-local, object-style property
#   - ?pattern_uri : IRI of an OWL characteristic type explicitly declared for
#                    ?prop_uri (may be unbound)
#
# NOTES     :
#   - Pure SPARQL 1.1; no vendor extensions.
#   - Properties with no declared OWL characteristics are still returned, with
#     ?pattern_uri left unbound.
#   - Pattern normalization (e.g. stripping "Property" suffix) is handled in
#     Python, not in SPARQL.
# --------------------------------------------------------------------------------------------------

PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX owl: <http://www.w3.org/2002/07/owl#>
PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>

SELECT DISTINCT ?prop_uri ?pattern_uri
WHERE {
  # @RELATIONS_SEED

  OPTIONAL {
    ?prop_uri a ?pattern_uri .
    VALUES ?pattern_uri {
      owl:ReflexiveProperty
      owl:IrreflexiveProperty
      owl:SymmetricProperty
      owl:AsymmetricProperty
      owl:TransitiveProperty
      owl:FunctionalProperty
      owl:InverseFunctionalProperty
    }
  }
}
ORDER BY STR(?prop_uri) COALESCE(STR(?pattern_uri), "")
