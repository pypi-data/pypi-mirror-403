$ Mesh Specification
mesh     rect nx=3 ny=60
y.m      n=1  l=0 r=1
y.m      n=10  l=0.002 r=0.8
y.m      n=60  l=0.6 r=1.25
x.m      n=1 l=0 r=1
x.m      n=3 l=1 r=1
$ The y.m nodepoints and distance are inputs
$ The x.m parameters are static

$ Regions specification
region   num=1 ix.l=1 ix.h=3 iy.l=1 iy.h=10 name=sio2 INS
region   num=2 ix.l=1 ix.h=3 iy.l=10 iy.h=60 name=silicon SEMI

$ Electrodes specification
elec     num=1 ix.l=1 ix.h=3 iy.l=1 iy.h=1
elec     num=2 ix.l=1 ix.h=3 iy.l=60 iy.h=60

$ Doping specification
dop reg=2 p.type conc=1e+18 uniform
$ doping can be p.type or n.type (two options), conc is a parameter too

$ Contact specification
contact all neutral
contact num=1 n.polysilicon
$ you can add as options: n.polysilicon, p.polysilicon tungsten

$ Material lifetime specification
material name=silicon
material name=sio2 permi=3.9 qf=0

$ Specify models
models TEMP=300 srh conmob fldmob
system electrons holes newton

$ Solve for initial conditions
solve init
plot.1d pot a.y=0 b.y=0.6 a.x=0.5 b.x=0.5 ascii outf=pot
plot.1d qfn a.y=0 b.y=0.6 a.x=0.5 b.x=0.5 ascii outf=qfn
plot.1d qfp a.y=0 b.y=0.6 a.x=0.5 b.x=0.5 ascii outf=qfp
plot.1d band.val a.y=0 b.y=0.6 a.x=0.5 b.x=0.5 ascii outf=val
plot.1d band.con a.y=0 b.y=0.6 a.x=0.5 b.x=0.5 ascii outf=con
plot.1d ele a.y=0 b.y=0.6 a.x=0.5 b.x=0.5 ascii outf=ele
plot.1d hole a.y=0 b.y=0.6 a.x=0.5 b.x=0.5 ascii outf=hole
plot.1d net.charge a.y=0 b.y=0.6 a.x=0.5 b.x=0.5 ascii outf=ro
plot.1d e.field a.y=0 b.y=0.6 a.x=0.5 b.x=0.5 ascii outf=efield

$ Solve for applied bias
solve v1=0 vstep=-0.2 nstep=10 elect=1
log acfile=ac
solve v1=-2.0 vstep=0.2 nstep=20 elect=1
+     ac freq=1e7
log off

plot.1d pot a.y=0 b.y=0.6 a.x=0.5 b.x=0.5 ascii outf=potiv
plot.1d qfn a.y=0 b.y=0.6 a.x=0.5 b.x=0.5 ascii outf=qfniv
plot.1d qfp a.y=0 b.y=0.6 a.x=0.5 b.x=0.5 ascii outf=qfpiv
plot.1d band.val a.y=0 b.y=0.6 a.x=0.5 b.x=0.5 ascii outf=valiv
plot.1d band.con a.y=0 b.y=0.6 a.x=0.5 b.x=0.5 ascii outf=coniv
plot.1d ele a.y=0 b.y=0.6 a.x=0.5 b.x=0.5 ascii outf=eleiv
plot.1d hole a.y=0 b.y=0.6 a.x=0.5 b.x=0.5 ascii outf=holeiv
plot.1d net.charge a.y=0 b.y=0.6 a.x=0.5 b.x=0.5 ascii outf=roiv
plot.1d e.field a.y=0 b.y=0.6 a.x=0.5 b.x=0.5 ascii outf=efieldiv

end